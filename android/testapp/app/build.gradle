plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {
    namespace 'io.github.aidenkoog.testapp'
    compileSdk 34

    defaultConfig {
        applicationId "io.github.aidenkoog.testapp"
        minSdk 27
        targetSdk 34
        /*
         * -----------------------------------------------------------------------------------------
         * major.minor.hotfix description
         * 1) major (feature) : feature adding
         * 2) minor (major) : critical modification
         * 3) hotfix
         * -----------------------------------------------------------------------------------------
         */
        versionCode 108
        versionName "1.0.8"
    }

    buildTypes {
        release {
            applicationIdSuffix ""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [appLabel: "Cooper-Test-App"]
        }
        debug {
            applicationIdSuffix ".dev"
            manifestPlaceholders = [appLabel: "Cooper-Test-App-DEV"]
        }
        stage {
            applicationIdSuffix ".stage"
            manifestPlaceholders = [appLabel: "Cooper-Test-App-STAGE"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def project = "CooperTestApp"
            def SEP = "_"
            def buildType = variant.buildType.name
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            def date = new Date()
            def formattedDate = date.format('yyyyMMdd_HHmm')
            def newApkName = project + SEP + buildType.toUpperCase() + SEP + versionName + SEP + versionCode + SEP + formattedDate + ".apk"
            outputFileName = new File(newApkName)
        }
    }
}

dependencies {

    // Compose UI
    implementation 'androidx.activity:activity-compose:1.7.2'

    // Google Material
    implementation 'com.google.android.material:material:1.9.0'

    // Lottie
    implementation 'com.airbnb.android:lottie:5.2.0'

    // Logger
    implementation 'com.orhanobut:logger:2.2.0'

    // Room
    implementation 'androidx.room:room-runtime:2.5.2'
    implementation 'androidx.room:room-rxjava2:2.5.2'
    kapt 'androidx.room:room-compiler:2.5.2'
}